<?xml version="1.0" encoding="utf-8"?>
<mx:VBox xmlns:mx="http://www.adobe.com/2006/mxml"
	creationComplete="init()"
	xmlns:common="com.cleartext.esm.views.common.*"
	width="100%">
	<mx:Script>
		<![CDATA[
			import com.cleartext.esm.assets.Constants;
			import mx.events.CollectionEvent;
			import mx.core.UIComponent;
			import com.cleartext.esm.events.PopUpEvent;
			import org.swizframework.Swiz;
			import com.cleartext.esm.views.common.SideButton;
			import mx.controls.Button;
			import com.cleartext.esm.events.BuddyModelEvent;
			import com.cleartext.esm.models.BuddyModel;
			
			[Autowire]
			[Bindable]
			public var buddies:BuddyModel;
			
			[Autowire (bean="xmpp", property="connected")]
			[Bindable]
			public var connected:Boolean;
			
			private var groupButtons:Dictionary = new Dictionary();
			
			private function init():void
			{
				buddies.groups.addEventListener(CollectionEvent.COLLECTION_CHANGE, groupListChanged);
				groupListChanged(null);
			}
			
			public function groupListChanged(event:CollectionEvent):void
			{
				var button:SideButton;
				
				var buttonsToRemove:Dictionary = new Dictionary();
				
				for each(button in groupButtons)
					buttonsToRemove[button.text] = button;
				
				var childIndex:int = 3;
				for each(var group:String in buddies.groups)
				{
					button = groupButtons[group] as SideButton;
					
					if(!button)
					{
						button = new SideButton();
						groupButtons[group] = button;
						button.text = group;
						button.icon = Constants.DefaultGroupIcon;
						button.addEventListener(MouseEvent.CLICK, clickHandler);
						addChildAt(button, childIndex);
					}
					else
					{
						setChildIndex(button, childIndex);
						delete buttonsToRemove[button.text];
					}
					childIndex++;
				}

				for each(button in buttonsToRemove)
				{
					removeChild(button);
					delete groupButtons[button.text];
				}
			}
			
			private function clickHandler(event:MouseEvent):void
			{
				buddies.groupName = event.target.text;
				invalidateProperties();
			}
			
			override protected function commitProperties():void
			{
				super.commitProperties();
				
				for each(var child:UIComponent in getChildren())
				{
					var sb:SideButton = child as SideButton;
					if(sb)
						sb.selected = sb.text==buddies.groupName;
				}
			}

		]]>
	</mx:Script>
	
	<common:SideButton text="{BuddyModel.OPEN_TABS}" click="clickHandler(event)" icon="{Constants.Tabs}" showEditButton="false"/>
	<common:SideButton text="{BuddyModel.MICRO_BLOGGING_GROUP}" click="clickHandler(event)" icon="{Constants.AllSocial}" showEditButton="false"/>
	<common:SideButton text="{BuddyModel.ALL_BUDDIES_GROUP}" click="clickHandler(event)" icon="{Constants.AllBuddies}" showEditButton="false"/>
	<common:SideButton text="{BuddyModel.GATEWAY_GROUP}" click="clickHandler(event)" icon="{Constants.Gateways}" showEditButton="false"/>
	<common:SideButton text="{BuddyModel.UNASIGNED}" click="clickHandler(event)" icon="{Constants.NoGroup}" showEditButton="false"/>
	<common:SideButton text="{BuddyModel.CHAT_ROOMS}" click="clickHandler(event)" icon="{Constants.DefaultGroupIcon}" showEditButton="false"/>

</mx:VBox>
