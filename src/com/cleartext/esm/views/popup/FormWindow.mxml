<?xml version="1.0" encoding="utf-8"?>
<popup:PopupWindowBase
	xmlns:fx="http://ns.adobe.com/mxml/2009" 
	xmlns:s="library://ns.adobe.com/flex/spark" 
	xmlns:mx="library://ns.adobe.com/flex/mx"
	xmlns:popup="com.cleartext.esm.views.popup.*"
	title="Edit Group"
	submitButtonLabel="Submit" >
	
	<fx:Script>
		<![CDATA[
			import mx.controls.Label;
			import mx.controls.TextInput;
			import mx.controls.TextArea;
			import mx.controls.CheckBox;
			import com.adobe.protocols.dict.Dict;
			import mx.core.UIComponent;
			import mx.formatters.SwitchSymbolFormatter;
			import mx.containers.FormItem;
			import com.cleartext.esm.models.valueObjects.FormField;
			import com.cleartext.esm.models.valueObjects.FormObject;
			import mx.events.FlexEvent;
			import mx.collections.ArrayCollection;
			import com.cleartext.esm.models.valueObjects.Buddy;
			import com.cleartext.esm.models.BuddyModel;
			import mx.events.CloseEvent;
			import com.cleartext.esm.models.XMPPModel;
			import com.cleartext.esm.models.DatabaseModel;
			import com.cleartext.esm.models.SettingsModel;
			import com.cleartext.esm.models.ApplicationModel;

			[Autowire]
			[Bindable]
			public var xmpp:XMPPModel;
			
			public var form:FormObject;
		
			private var vars:Dictionary = new Dictionary();
			private var required:Array = new Array();
			
			override protected function init(event:Event):void
			{
				title = "Request from " + form.from;
				instructions.text = form.instructions;
				
				for each(var field:FormField in form.fields)
				{
					var item:FormItem = new FormItem();
					item.label = field.label;
					
					var child:DisplayObject;
					switch(field.type)
					{
						case "fixed" :
							child = new Text();
							(child as Text).text = field.value.toString();
							break;
						case "text-multi" :
							child = new TextArea();
							child.height = 75;
							(child as TextArea).text = field.value.toString();
							break;
						case "text-private" :
							child = new TextInput();
							(child as TextInput).text = field.value.toString();
							(child as TextInput).displayAsPassword = true;
							break;
						case "text-single" :
							child = new TextInput();
							(child as TextInput).text = field.value.toString();
							break;
					}
					
					if(child)
					{
						child.width = 200;
						item.addChild(child);
						mxmlForm.addChild(item);
	
						if(field.varName)
							vars[field.varName] = child;
	
						if(field.required)
						{
							child.addEventListener(Event.CHANGE, validateInput);
							child.addEventListener(FocusEvent.FOCUS_OUT, validateInput);
							required.push(child);
						}
					}
				}
				
				isValid = required.length == 0;
			}
			
			override protected function submit(event:Event):void
			{
				for each(var field:FormField in form.fields)
				{
					if(field.varName)
					{
						var child:Object = vars[field.varName];
						if(child.hasOwnProperty("text"))
							field.value = child.text;
					}
				}
				
				xmpp.submitForm(form);
				
				closeWindow();
			}
			
			override protected function validateForm():void
			{
				for each(var child:Object in required)
				{
					if(child.hasOwnProperty("text") && child.text=="")
					{
						isValid = false;
						return;
					}
				}
				isValid = true;
			}
		]]>
	</fx:Script>
	
    <mx:Form id="mxmlForm">
		<mx:FormItem>
			<mx:Text width="200" id="instructions"/>
		</mx:FormItem>
	</mx:Form>
    
	<popup:controlBarContent />
	
</popup:PopupWindowBase>