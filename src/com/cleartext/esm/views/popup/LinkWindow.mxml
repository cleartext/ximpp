<?xml version="1.0" encoding="utf-8"?>
<popup:PopupWindowBase
	xmlns:fx="http://ns.adobe.com/mxml/2009" 
	xmlns:s="library://ns.adobe.com/flex/spark" 
	xmlns:mx="library://ns.adobe.com/flex/mx"
	xmlns:popup="com.cleartext.esm.views.popup.*"
	title="Checking for short urls..."
	submitButtonLabel="Visit link"
	isValid="true"
	>
	
	<fx:Script>
		<![CDATA[
			import com.cleartext.esm.events.LinkEvent;
			import com.cleartext.esm.models.ExpandUrlModel;
			import com.cleartext.esm.models.utils.LinkUitls;
			
			import mx.events.FlexEvent;
			import mx.rpc.events.ResultEvent;
			
			[Autowire]
			[Bindable]
			public var webRoot:ExpandUrlModel;
			
			[Bindable]
			public var url:String;
			
			private var idCounter:int;
			private var redirecting:Boolean = false;
			
			override protected function submit(event:Event):void
			{
				if(closing)
					return;

				var request:URLRequest = new URLRequest(url);
				navigateToURL(request);
				closeWindow();
			}
			
			override protected function init(event:Event):void
			{
				textArea.htmlText='<B>' + url + '</B><BR/>';
				idCounter = webRoot.checkUrl(url);
			}
			
			[Mediate (event="LinkEvent.LINK_RESULT")]
			public function linkResultHandler(event:LinkEvent):void
			{
				if(closing)
					return;
				
				// this is not the result we are looking for
				if(event.id != idCounter)
					return;
					
				if(event.status == LinkEvent.REDIRECT)
				{
					url = event.urlOrMessage;
					idCounter = webRoot.checkUrl(url);
					textArea.htmlText += "<br/><b>" + (redirecting ? "Which&nbsp;r" : "R") + "edirects&nbsp;to:</b>&nbsp;" + url ;
					redirecting = true;
				}
				else if(event.status == LinkEvent.ERROR)
				{
					textArea.htmlText += "<br/><FONT color='#FF3333'><b>ERROR:</b></FONT> " + event.urlOrMessage;
				}
				else if(!redirecting)
				{
					submit(null);
				}
			}
			
		]]>
	</fx:Script>

	<mx:Text width="300" height="100%" id="textArea" />
	
	<popup:controlBarContent />
	
</popup:PopupWindowBase>