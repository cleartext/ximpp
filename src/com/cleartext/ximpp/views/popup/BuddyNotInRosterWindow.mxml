<?xml version="1.0" encoding="utf-8"?>
<popup:PopupWindowBase
	xmlns:popup="com.cleartext.ximpp.views.popup.*"
	xmlns:mx="http://www.adobe.com/2006/mxml" 
	title="Buddy Not In Buddy List"
	submitButtonLabel="Add Buddy"
	isValid="true">
	
	<mx:Script>
		<![CDATA[
			import com.cleartext.ximpp.models.valueObjects.Message;
			import flash.sampler.NewObjectSample;
			import com.cleartext.ximpp.models.valueObjects.Buddy;
			import com.cleartext.ximpp.models.BuddyModel;
			import com.cleartext.ximpp.models.types.SubscriptionTypes;
			import mx.events.CloseEvent;
			import com.cleartext.ximpp.models.XMPPModel;
			import com.cleartext.ximpp.models.DatabaseModel;
			import com.cleartext.ximpp.models.SettingsModel;
			import com.cleartext.ximpp.models.ApplicationModel;

			[Autowire]
			[Bindable]
			public var xmpp:XMPPModel;

			[Autowire]
			[Bindable]
			public var buddies:BuddyModel;
			
			override protected function submit(event:Event):void
			{
				buddy.nickName = nickname.text;
				buddies.addBuddy(buddy);
				buddies.refresh();
				
				xmpp.modifyRosterItem(buddy);
				
				if(subscriptionPanel.subscribeYes.selected)
					xmpp.sendSubscribe(buddy.jid, SubscriptionTypes.SUBSCRIBE);
				
				closeWindow();
			}
			
			private var _buddy:Buddy;
			public function get buddy():Buddy
			{
				return _buddy;
			}
			public function set buddy(value:Buddy):void
			{
				_buddy = value;
				jid.text = buddy.jid;
				nickname.text = buddy.nickName;

				subscriptionPanel.buddy = buddy;
				subscriptionPanel.subscribeYes.selected = true;
				invalidateProperties();
			}
			
			private var _messageString:String;
			public function get messageString():String
			{
				return _messageString;
			}
			public function set messageString(value:String):void
			{
				_messageString = value;
				invalidateProperties();
			}
			
			override protected function commitProperties():void
			{
				super.commitProperties();
				
				if(!buddy)
					return;
				
				if(messageString)
				{
					explanationText.text = "You have received a presence stanza from " + 
						buddy.nickName + " of type " + messageString + ", do you want to add them to your buddy list?";
				}
				else
				{
					explanationText.text = "You have received a message from " + 
						buddy.nickName + ", do you want to add them to your buddy list?";
				}
			}
			
		]]>
	</mx:Script>
	
	<mx:Form>

		<mx:Text id="explanationText"  width="450"/>
		
		<mx:FormItem label="jid:">
			<mx:Label id="jid" />
		</mx:FormItem>

		<mx:FormItem label="nickname:">
			<mx:TextInput id="nickname" />
		</mx:FormItem>

		<popup:SubscirptionPanel id="subscriptionPanel" showPublish="false" />
		
	</mx:Form>
	
	<mx:ControlBar />

</popup:PopupWindowBase>
