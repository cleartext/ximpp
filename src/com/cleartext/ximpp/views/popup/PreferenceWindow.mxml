<?xml version="1.0" encoding="utf-8"?>
<popup:PopupWindowBase
	xmlns:popup="com.cleartext.ximpp.views.popup.*"
	width="500" height="100%"
	xmlns:mx="http://www.adobe.com/2006/mxml"
	title="User Preferences"
	layout="absolute"
	xmlns:preferences="com.cleartext.ximpp.views.preferences.*" 
	xmlns:common="com.cleartext.ximpp.views.common.*" 
	submitButtonLabel="save"
	isValid="true"
	>
	
	<mx:Script>
		<![CDATA[
			import mx.events.FlexEvent;
			import com.cleartext.ximpp.models.BuddyModel;
			import com.adobe.crypto.SHA1;
			import com.cleartext.ximpp.models.valueObjects.Status;
			import com.cleartext.ximpp.models.utils.AvatarUtils;
			import com.cleartext.ximpp.events.BuddyEvent;
			import mx.events.CollectionEvent;
			import com.cleartext.ximpp.models.XMPPModel;
			import com.cleartext.ximpp.models.DatabaseModel;
			import com.cleartext.ximpp.models.SettingsModel;
			import com.cleartext.ximpp.models.ApplicationModel;
			import com.cleartext.ximpp.models.valueObjects.UrlShortener;
			import com.cleartext.ximpp.models.valueObjects.UserAccount;
			import mx.utils.ObjectUtil;
			import mx.controls.Alert;
			import mx.graphics.codec.JPEGEncoder;
			import mx.collections.ArrayCollection;
			
			public var changesMade:Boolean = false;
			
			private var previousAccount:UserAccount;
			private function get accountCollection():ArrayCollection
			{
				return accountList.dataProvider as ArrayCollection;
			}
			
			private var accountsToDelete:Array = new Array();
			
			[Autowire]
			[Bindable]
			public var appModel:ApplicationModel;
			
			[Autowire]
			[Bindable]
			public var settings:SettingsModel;
			
			[Autowire]
			[Bindable]
			public var database:DatabaseModel;
			
			[Autowire]
			[Bindable]
			public var xmpp:XMPPModel;
			
			[Autowire]
			[Bindable]
			public var buddies:BuddyModel;
			
			override protected function init(event:FlexEvent):void
			{
				// set global values
				autoConnectCheckBox.selected = settings.global.autoConnect;
				urlShortenComboBox.selectedItem = settings.global.urlShortener;
				animateBuddyListCheckBox.selected = settings.global.animateBuddyList;
				animateMessageListCheckBox.selected = settings.global.animateMessageList;
				showOfflineBuddiesCheckBox.selected = settings.global.showOfflineBuddies;
				numTimelineMessages.value = settings.global.numTimelineMessages;
				numChatMessages.value = settings.global.numChatMessages;
				
				// load user accounts
				accountList.dataProvider = database.getAllUserAccounts();
				
				for each(var userAcount:UserAccount in accountCollection)
				{
					if(settings.userAccount && userAcount.userId == settings.userAccount.userId)
					{
						accountList.selectedItem = userAcount;
						break;
					}
				}
				
				if(appModel.serverSideStatus.value == Status.CONNECTING)
					accountHBox.enabled = false;
	
				selectedAccountChanged();
			}
		
			override protected function submit(event:Event):void
			{
				selectedAccountChanged();
				
				// commit global values
				settings.global.autoConnect = autoConnectCheckBox.selected;
				settings.global.urlShortener = urlShortenComboBox.selectedItem as String;
				settings.global.animateBuddyList = animateBuddyListCheckBox.selected;
				settings.global.animateMessageList = animateMessageListCheckBox.selected;
				settings.global.showOfflineBuddies = showOfflineBuddiesCheckBox.selected;
				settings.global.numTimelineMessages = numTimelineMessages.value;
				settings.global.numChatMessages = numChatMessages.value;
				
				for each(var userId:int in accountsToDelete)
					database.removeAccount(userId);
				
				for each(var userAccount:UserAccount in accountList.dataProvider)
					database.saveUserAccount(userAccount);
				
				settings.userAccount = accountList.selectedItem as UserAccount;
				
				database.saveGlobalSettings();

				buddies.refresh();
				
				closeWindow();
			}
			
			private function selectedAccountChanged():void
			{
				if(previousAccount)
				{
					previousAccount.accountName = accountName.text;
					previousAccount.nickName = nickName.text;
					previousAccount.avatar = avatar.bitmapData;
					previousAccount.jid = jid.text;
					previousAccount.password = password.text;
					previousAccount.server = server.text;
				}
				
				previousAccount = accountList.selectedItem as UserAccount;
				
				if(previousAccount)
				{
					accountName.text = previousAccount.accountName;
					nickName.text = previousAccount.nickName;
					avatar.data = previousAccount;
					jid.text = previousAccount.jid;
					password.text = previousAccount.password;
					server.text = previousAccount.server;
				}
				else
				{
					addAccount();
				}
				
				accountCollection.refresh();
			}
			
			private function addAccount():void
			{
				var account:UserAccount = new UserAccount();
				accountCollection.addItem(account);
				accountList.selectedItem = account;
				selectedAccountChanged();
			}
			
			private function removeAccount():void
			{
				if(accountCollection.length == 1)
				{
					Alert.show("You must have at least one user account", "Can not delete account");
					return;
				}
					
				var index: int = accountList.selectedIndex;
				accountCollection.removeItemAt(index);
				accountsToDelete.push(previousAccount.userId);
				previousAccount = null;
				
				if(accountCollection.length > 0)
				{
					index = Math.max(0, index-1);
					accountList.selectedItem = accountCollection.getItemAt(index);
				}
				
				selectedAccountChanged();
			}
			
			private function changeAvatar():void
			{
				try 
				{
					var newImage:File = new File();
					newImage.browseForOpen("Open",  [new FileFilter("Images", "*.jpg;*.gif;*.png")]);
					newImage.addEventListener(Event.SELECT, 
						function():void
						{
							changesMade = true;
							var filestream:FileStream=new FileStream();
							filestream.open(newImage,"read");
							var bytesRead:ByteArray = new ByteArray();
							filestream.readBytes(bytesRead, 0, filestream.bytesAvailable);
							var loader:Loader = new Loader();
							loader.loadBytes(bytesRead);
							loader.contentLoaderInfo.addEventListener(Event.COMPLETE,
								function():void
								{
									var loadedBitmap:Bitmap = loader.content as Bitmap;
									var newBitmapData:BitmapData = new BitmapData(AvatarUtils.AVATAR_SIZE, AvatarUtils.AVATAR_SIZE);
									var scale:Number = AvatarUtils.AVATAR_SIZE / Math.max(loadedBitmap.width, loadedBitmap.height);
									var tx:Number = (AvatarUtils.AVATAR_SIZE - loadedBitmap.width * scale)/2;
									var ty:Number = (AvatarUtils.AVATAR_SIZE - loadedBitmap.height * scale)/2;
									newBitmapData.draw(loadedBitmap, new Matrix(scale, 0, 0, scale, tx, ty));
									if(previousAccount)
									{
										previousAccount.avatar = newBitmapData;
										previousAccount.avatarHash = SHA1.hashBytes(newBitmapData.getPixels(newBitmapData.rect));
									}
								});
						});
				}
				catch (error:Error)
				{
					appModel.log(error);
				}
			}
			
			
		]]>
	</mx:Script>
	
	<mx:Label x="10" y="10" styleName="blackBoldBig" text="Global Settings" />

	<mx:HRule left="10" right="10" y="35" />

	<mx:Canvas x="10" y="45">
		<mx:CheckBox label="auto connect" id="autoConnectCheckBox"/>
		<mx:CheckBox y="30" label="show offline buddies" id="showOfflineBuddiesCheckBox"/>
		<mx:CheckBox y="60" label="animate buddy list" id="animateBuddyListCheckBox"/>
		<mx:CheckBox y="90" label="animate message list" id="animateMessageListCheckBox"/>
	
		<mx:Canvas x="195">
			<mx:NumericStepper maximum="1000" minimum="0" id="numTimelineMessages" />
			<mx:Label x="78" text="max timeline messages" />
		</mx:Canvas>
		<mx:Canvas x="195" y="30">
			<mx:NumericStepper maximum="1000" minimum="0" id="numChatMessages" />
			<mx:Label x="78" text="max chat messages" />
		</mx:Canvas>
		<mx:Canvas x="195" y="60">
			<mx:ComboBox id="urlShortenComboBox" dataProvider="{UrlShortener.types}"/>
			<mx:Label x="{urlShortenComboBox.width+4}" text="url shorten service:" y="4"/>
		</mx:Canvas>
	</mx:Canvas>

	<mx:Label x="10" y="180" styleName="blackBoldBig" text="Account Settings" />
	
	<mx:HRule left="10" right="10" y="205" />

	<mx:Canvas id="accountHBox" y="215" right="10" left="10" height="240" enabled="{!xmpp.connected}">
		<mx:List id="accountList" top="10" bottom="40" width="180" horizontalScrollPolicy="off" labelField="accountName" change="selectedAccountChanged()"/>
		<popup:AddRemoveButton
			bottom="15"
			width="180"
			addButtonClicked="addAccount()"
			removeButtonClicked="removeAccount()"/>
		
		<mx:Form right="0" height="100%">
			<mx:FormItem label="account name:">
				<mx:VBox>
					<mx:TextInput id="accountName"
						focusOut="validateInput(event, 'account name can not be empty')" 
						change="changesMade = true; validateInput(event, 'account name can not be empty'); if(previousAccount) { previousAccount.accountName = accountName.text; accountCollection.refresh(); }"/>
				</mx:VBox>
			</mx:FormItem>
			<mx:FormItem label="nick name:">
				<mx:TextInput id="nickName" change="changesMade = true;" />
			</mx:FormItem>
			<mx:FormItem label="profile picture:" direction="horizontal">
				<common:Avatar id="avatar" width="{AvatarUtils.AVATAR_SIZE}" height="{AvatarUtils.AVATAR_SIZE}" buttonMode="true" editClicked="changeAvatar()" />
			</mx:FormItem>
			<mx:FormItem label="jid:">
				<mx:VBox>
					<mx:TextInput id="jid" change="changesMade = true; validateInput(event, 'jid can not be empty');" focusOut="validateInput(event, 'jid can not be empty'); " />
				</mx:VBox>
			</mx:FormItem>
			<mx:FormItem label="password:">
				<mx:VBox>
					<mx:TextInput id="password" displayAsPassword="true" change="changesMade = true; validateInput(event, 'password can not be empty');" focusOut="validateInput(event, 'password can not be empty'); " />
				</mx:VBox>
			</mx:FormItem>
			<mx:FormItem label="server:">
				<mx:VBox>
					<mx:TextInput id="server" change="changesMade = true; validateInput(event, 'server can not be empty');" focusOut="validateInput(event, 'server can not be empty'); "  />
				</mx:VBox>
			</mx:FormItem>
		</mx:Form>
	</mx:Canvas>
	
	<mx:Canvas visible="{!accountHBox.enabled}" width="210" horizontalCenter="0" y="280" height="50" backgroundColor="0xffffff" borderStyle="solid" borderColor="0x444444">
		<mx:Text text="please log off to edit account settings" textAlign="center" horizontalCenter="0" verticalCenter="0"/>
	</mx:Canvas>
	
	<mx:Label x="10" y="500" styleName="blackBoldBig" text="Micro Blogging Settings" />

	<mx:HRule left="10" right="10" y="525" />

<!--	<mx:Canvas x="10" y="535">
		<mx:Form>
			<mx:FormItem label="nick name:">
				<mx:VBox>
					<mx:TextInput id="displayName" change="changesMade = true;" />
				</mx:VBox>
			</mx:FormItem>
			<mx:FormItem label="nick name:">
				<mx:TextInput id="nickName" change="changesMade = true;" />
			</mx:FormItem>
			<mx:FormItem label="profile picture:" direction="horizontal">
				<common:Avatar id="avatar" width="{AvatarUtils.AVATAR_SIZE}" height="{AvatarUtils.AVATAR_SIZE}" buttonMode="true" editClicked="changeAvatar()" />
			</mx:FormItem>
			<mx:FormItem label="jid:">
				<mx:VBox>
					<mx:TextInput id="jid" change="changesMade = true; validateInput(event, 'jid can not be empty');" focusOut="validateInput(event, 'jid can not be empty'); " />
				</mx:VBox>
			</mx:FormItem>
			<mx:FormItem label="password:">
				<mx:VBox>
					<mx:TextInput id="password" displayAsPassword="true" change="changesMade = true; validateInput(event, 'password can not be empty');" focusOut="validateInput(event, 'password can not be empty'); " />
				</mx:VBox>
			</mx:FormItem>
			<mx:FormItem label="server:">
				<mx:VBox>
					<mx:TextInput id="server" change="changesMade = true; validateInput(event, 'server can not be empty');" focusOut="validateInput(event, 'server can not be empty'); "  />
				</mx:VBox>
			</mx:FormItem>
		</mx:Form>
	</mx:Canvas>
-->
	<mx:ControlBar/>

</popup:PopupWindowBase>
