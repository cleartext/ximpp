<?xml version="1.0" encoding="utf-8"?>
<mx:TitleWindow 
	xmlns:mx="http://www.adobe.com/2006/mxml" 
	layout="absolute"
	showCloseButton="true"
	title="Edit Buddy"
	xmlns:common="com.cleartext.ximpp.views.common.*">
	
	<mx:Script>
		<![CDATA[
			import com.cleartext.ximpp.models.BuddyModel;
			import mx.controls.Alert;
			import com.cleartext.ximpp.models.AvatarUtils;
			import com.cleartext.ximpp.models.valueObjects.Buddy;
			import com.cleartext.ximpp.models.types.SubscriptionTypes;
			import mx.events.CloseEvent;
			import com.cleartext.ximpp.models.XMPPModel;
			import com.cleartext.ximpp.models.DatabaseModel;
			import com.cleartext.ximpp.models.SettingsModel;
			import com.cleartext.ximpp.models.ApplicationModel;

			[Autowire]
			[Bindable]
			public var xmpp:XMPPModel;
			
			[Autowire]
			[Bindable]
			public var buddies:BuddyModel;
			
			private function okHandler():void
			{
				buddy.nickName = nickname.text;
				buddy.groups = groups.selectedItems;
				buddies.addBuddy(buddy);
				buddies.refresh();
				
				xmpp.modifyRosterItem(buddy);
				
				dispatchEvent(new CloseEvent(CloseEvent.CLOSE));
			}
			
			private var _buddy:Buddy;
			public function get buddy():Buddy
			{
				return _buddy;
			}
			public function set buddy(value:Buddy):void
			{
				_buddy = value;
				jid.text = buddy.jid;
				nickname.text = buddy.nickName;
				avatar.data = buddy;
				subscription.text = buddy.subscription;
				groups.enabled = !buddy.isGateway && !buddy.microBlogging;
				groups.selectedItems = buddy.groups;
				avatar.buttonMode = (buddy.avatarHash == "" || buddy.avatarHash == null);
			}
			
			private function changeAvatar():void
			{
				try 
				{
					var newImage:File = new File();
					newImage.browseForOpen("Open",  [new FileFilter("Images", "*.jpg;*.gif;*.png")]);
					newImage.addEventListener(Event.SELECT, 
						function():void
						{
							var filestream:FileStream=new FileStream();
							filestream.open(newImage,"read");
							var bytesRead:ByteArray = new ByteArray();
							filestream.readBytes(bytesRead, 0, filestream.bytesAvailable);
							var loader:Loader = new Loader();
							loader.loadBytes(bytesRead);
							loader.contentLoaderInfo.addEventListener(Event.COMPLETE,
								function():void
								{
									var loadedBitmap:Bitmap = loader.content as Bitmap;
									var newBitmapData:BitmapData = new BitmapData(AvatarUtils.AVATAR_SIZE, AvatarUtils.AVATAR_SIZE);
									var scale:Number = AvatarUtils.AVATAR_SIZE / Math.max(loadedBitmap.width, loadedBitmap.height);
									var tx:Number = (AvatarUtils.AVATAR_SIZE - loadedBitmap.width * scale)/2;
									var ty:Number = (AvatarUtils.AVATAR_SIZE - loadedBitmap.height * scale)/2;
									newBitmapData.draw(loadedBitmap, new Matrix(scale, 0, 0, scale, tx, ty));
									buddy.avatar = newBitmapData;
								});
						});
				}
				catch (error:Error)
				{
					// to do
				}
			}
		]]>
	</mx:Script>
	
	<mx:Form>
		
		<mx:FormItem label="jid:">
			<mx:Label id="jid" />
		</mx:FormItem>
		
		<mx:FormItem label="avatar:">
			<common:Avatar id="avatar" width="{AvatarUtils.AVATAR_SIZE}" height="{AvatarUtils.AVATAR_SIZE}" editClicked="changeAvatar()"/>
		</mx:FormItem>

		<mx:FormItem label="subscription:">
			<mx:Label id="subscription" />
		</mx:FormItem>

		<mx:FormItem label="groups:">
			<mx:Label text="use ctrl/cmd to select more than one" />
			<mx:List
				id="groups" 
				allowMultipleSelection="true" 
				dataProvider="{buddies.groups}" 
				width="190" height="80"/>
		</mx:FormItem>

		<mx:FormItem label="nickname:">
			<mx:TextInput id="nickname" />
		</mx:FormItem>

	</mx:Form>

	<mx:ControlBar horizontalAlign="center">
		<mx:Button label="done" click="okHandler()"/>
		<mx:Button label="cancel" click="dispatchEvent(new CloseEvent(CloseEvent.CLOSE))"/>
	</mx:ControlBar>

</mx:TitleWindow>
