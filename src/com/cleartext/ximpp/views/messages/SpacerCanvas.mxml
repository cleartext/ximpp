<?xml version="1.0" encoding="utf-8"?>
<mx:Canvas xmlns:mx="http://www.adobe.com/2006/mxml"
	xmlns:common="com.cleartext.ximpp.views.common.*"
	borderStyle="none"
	horizontalScrollPolicy="off"
	verticalScrollPolicy="off"
	creationComplete="init();">
	<mx:Script>
		<![CDATA[
			import com.cleartext.ximpp.models.valueObjects.Message;
			import com.cleartext.ximpp.models.ApplicationModel;
			import com.cleartext.ximpp.events.SearchBoxEvent;
			import mx.events.CloseEvent;
			
			[Autowire]
			public var appModel:ApplicationModel;
			
			private var searchString:String;
			
			private function doSearch(event:SearchBoxEvent):void
			{
				searchString = event.searchString;
				appModel.microBloggingMessages.refresh();
			}
			
			private function init():void
			{
				appModel.microBloggingMessages.filterFunction = filterMessages;
			}
			
			private function filterMessages(message:Message):Boolean
			{
				return message.body.indexOf(searchString) != -1;
			}
			
			override protected function updateDisplayList(unscaledWidth:Number, unscaledHeight:Number):void
			{
				super.updateDisplayList(unscaledWidth, unscaledHeight);

				var g:Graphics = graphics;
				var w:Number = unscaledWidth;
				var h:Number = unscaledHeight;
				
				g.clear();

				var m:Matrix = new Matrix();
				m.createGradientBox(w, h, Math.PI/2, 0, 0);
				g.beginGradientFill(GradientType.LINEAR, [0xffffff, 0xeaeaea], [1, 1], [0x36, 0xFF], m);  
				g.drawRect(0, 0, w, h);
				
				g.endFill();
				g.lineStyle(1, 0xffffff);
				g.moveTo(0,h);
				g.lineTo(0,0);
				g.lineTo(w-1,0);
				g.lineTo(w-1,h);

				g.lineStyle(1, 0xcccccc);
				g.moveTo(1,h-1);
				g.lineTo(w-2,h-1);
			}
		]]>
	</mx:Script>

	<common:SearchBox width="400" height="24" x="12" y="6" search="doSearch(event)" borderAlpha="0.2"/>
	<mx:Label text="300 of 433" x="416" y="10"/>
	<mx:Button label="X" click="dispatchEvent(new CloseEvent(CloseEvent.CLOSE));" right="5" y="6"/>

</mx:Canvas>
