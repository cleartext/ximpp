<?xml version="1.0" encoding="utf-8"?>
<mx:Canvas
	width="100%" height="100%"
	xmlns:mx="http://www.adobe.com/2006/mxml"
	xmlns:header="com.cleartext.ximpp.views.header.*"
	xmlns:buddies="com.cleartext.ximpp.views.buddies.*" 
	xmlns:messages="com.cleartext.ximpp.views.messages.*"
	creationComplete="init()"
	>

	<mx:Script>
		<![CDATA[
			import com.cleartext.ximpp.views.popup.ChangePasswordWindow;
			import flash.net.navigateToURL;
			import com.cleartext.ximpp.views.popup.LinkWindow;
			import com.cleartext.ximpp.events.LinkEvent;
			import mx.core.Application;
			import mx.events.AIREvent;
			import com.cleartext.ximpp.events.ApplicationEvent;
			import com.cleartext.ximpp.views.popup.JoinGroupChatWindow;
			import com.cleartext.ximpp.models.SoundAndColorModel;
			import com.cleartext.ximpp.views.popup.SearchWindow;
			import com.cleartext.ximpp.views.popup.XmlInputWindow;
			import com.cleartext.ximpp.assets.Constants;
			import com.cleartext.ximpp.views.popup.PopupWindowBase;
			import mx.core.UIComponent;
			import com.cleartext.ximpp.views.popup.BroadcastWindow;
			import com.cleartext.ximpp.views.popup.AddGatewayWindow;
			import com.cleartext.ximpp.views.popup.EditGroupWindow;
			import com.cleartext.ximpp.views.popup.RemoveGroupWindow;
			import mx.events.FlexEvent;
			import com.cleartext.ximpp.views.popup.RemoveBuddyWindow;
			import com.cleartext.ximpp.models.XMPPModel;
			import com.cleartext.ximpp.events.SendButtonEvent;
			import org.swizframework.Swiz;
			import com.cleartext.ximpp.models.BuddyModel;
			import com.cleartext.ximpp.views.popup.PreferenceWindow;
			import mx.core.IFlexDisplayObject;
			import com.cleartext.ximpp.views.popup.EditBuddyWindow;
			import com.cleartext.ximpp.events.PopUpEvent;
			import com.cleartext.ximpp.views.popup.AddGroupWindow;
			import com.cleartext.ximpp.models.ApplicationModel;
			import mx.events.CloseEvent;
			import mx.managers.PopUpManager;
			import mx.controls.Alert;
			
			import mx.core.mx_internal;

			[Autowire]
			[Bindable]
			public var appModel:ApplicationModel;		
		
			[Autowire]
			[Bindable]
			public var buddies:BuddyModel;
			
			[Autowire]
			[Bindable]
			public var soundColor:SoundAndColorModel;
			
			[Autowire(bean="xmpp", property="connected")]
			[Bindable]
			public var xmppConnected:Boolean;
		
			private function init():void
			{
				Application.application.addEventListener(AIREvent.APPLICATION_ACTIVATE, applicationActivateHandler);
			}
			
			private function applicationActivateHandler(event:AIREvent):void
			{
				NativeApplication.nativeApplication.icon.bitmaps = [Constants.appIconBmd];
			}
			
			//----------------------------------------------
			// MISC WINDOWS
			//----------------------------------------------
				
			[Mediate(event="PopUpEvent.CHANGE_PASSWORD_WINDOW")]
			public function showChangePasswordWindow(event:PopUpEvent):void
			{
				initializePopUp(new ChangePasswordWindow());
			}

			[Mediate(event="PopUpEvent.PREFERENCES_WINDOW")]
			public function showPreferencesWindow(event:PopUpEvent):void
			{
				initializePopUp(new PreferenceWindow());
			}

			[Mediate(event="LinkEvent.LINK_CLICKED")]
			public function showLinkWindow(event:LinkEvent):void
			{
				if(appModel.settings.global.checkUrls)
				{
					var window:LinkWindow = new LinkWindow();
					window.url = event.url;
					initializePopUp(window);
				}
				else
				{
					navigateToURL(new URLRequest(event.url));
				}
			}

			[Mediate(event="PopUpEvent.BROADCAST_WINDOW")]
			public function showSendToAllWindow(event:PopUpEvent):void
			{
				var window:BroadcastWindow = new BroadcastWindow();
				initializePopUp(window);

				window.group = event.buddy;
				window.message = event.messageString;
			}

			[Mediate(event="PopUpEvent.XML_INPUT_WINDOW")]
			public function showXmlInputWindow(event:PopUpEvent):void
			{
				initializePopUp(new XmlInputWindow());
			}
			
			[Mediate(event="PopUpEvent.SEARCH_WINDOW")]
			public function showSearchWindow(event:PopUpEvent):void
			{
				var window:SearchWindow = new SearchWindow();
				window.appModel = appModel;
				window.open();
			}

			[Mediate(event="PopUpEvent.JOIN_GROUP_CHAT")]
			public function showGroupChatWindow(event:PopUpEvent):void
			{
				var window:JoinGroupChatWindow = new JoinGroupChatWindow();
				initializePopUp(window);
			}

			//----------------------------------------------
			// BUDDY WINDOWS
			//----------------------------------------------
				
			[Mediate(event="PopUpEvent.ADD_BUDDY_WINDOW")]
			public function showAddBuddyWindow(event:PopUpEvent):void
			{
				initializePopUp(new EditBuddyWindow());
			}

			[Mediate(event="PopUpEvent.EDIT_BUDDY_WINDOW")]
			public function showEditBuddyWindow(event:PopUpEvent):void
			{
				var window:EditBuddyWindow = new EditBuddyWindow();
				initializePopUp(window);

				window.buddy = event.buddy;
			}

			[Mediate(event="PopUpEvent.DELETE_BUDDY_WINDOW")]
			public function showRemoveBuddyWindow(event:PopUpEvent):void
			{
				var window:RemoveBuddyWindow = new RemoveBuddyWindow();
				initializePopUp(window);

				window.buddy = event.buddy;
			}

			[Mediate(event="PopUpEvent.SUBSCRIPTION_REQUEST_WINDOW")]
			public function showSubscriptionRequest(event:PopUpEvent):void
			{
				var window:EditBuddyWindow = new EditBuddyWindow();
				initializePopUp(window);

				// the order of the next three asignments is important to 
				// set the correct values in the window - I know this is bad
				// but it saves a lot of code.
				window.buddy = event.buddy;
				window.presenceRequest = event.presenceRequest;
				window.messageString = event.messageString;
			}

			//----------------------------------------------
			// GROUP WINDOWS
			//----------------------------------------------
				
			[Mediate(event="PopUpEvent.ADD_GROUP_WINDOW")]
			public function showAddGroupWindow(event:PopUpEvent):void
			{
				initializePopUp(new AddGroupWindow());
			}

			[Mediate(event="PopUpEvent.EDIT_GROUP_WINDOW")]
			public function showEditGroupWindow(event:PopUpEvent):void
			{
				var window:EditGroupWindow = new EditGroupWindow();
				window.groupName = event.group;
				initializePopUp(window);
			}


			[Mediate(event="PopUpEvent.DELETE_GROUP_WINDOW")]
			public function showRemoveGroupWindow(event:PopUpEvent):void
			{
				var window:RemoveGroupWindow = new RemoveGroupWindow();
				window.groupName = event.group;
				initializePopUp(window);
			}

			//----------------------------------------------
			// GATEWAY WINDOWS
			//----------------------------------------------
				
			[Mediate(event="PopUpEvent.ADD_GATEWAY_WINDOW")]
			public function showAddGatewayWindow(event:PopUpEvent):void
			{
				initializePopUp(new AddGatewayWindow());
			}
			
			private function initializePopUp(window:IFlexDisplayObject):void
			{
				window.addEventListener(CloseEvent.CLOSE, 
					function():void
					{
						PopUpManager.removePopUp(window);
					});
				PopUpManager.addPopUp(window, this, true);
				PopUpManager.centerPopUp(window);
				((window as PopupWindowBase).defaultButton as Button).setFocus();
			}
			
			[Mediate(event="ApplicationEvent.NOTIFY")]
			public function notify(event:ApplicationEvent):void
			{
				NativeApplication.nativeApplication.icon.bitmaps = [Constants.appIconWarningBmd];
				
				if(NativeApplication.supportsDockIcon)
				{
					var dock:DockIcon = NativeApplication.nativeApplication.icon as DockIcon;
					dock.bounce(NotificationType.INFORMATIONAL);
				}
				else if (NativeApplication.supportsSystemTrayIcon)
				{
					stage.nativeWindow.notifyUser(NotificationType.INFORMATIONAL);
				}
			}

			override protected function updateDisplayList(unscaledWidth:Number, unscaledHeight:Number):void
			{
				super.updateDisplayList(unscaledWidth, unscaledHeight);
				
				var g:Graphics = graphics;
				g.clear();
	
				g.beginFill(soundColor.backgroundColor);
				g.drawRect(0, 0, unscaledWidth, unscaledHeight);
	
				var matrix:Matrix = new Matrix();

				var w:Number = unscaledWidth/2 - 100;
				var r:Number = Math.tan(Math.atan(w/50) - Math.atan(50/w)) * w;
				matrix.createGradientBox(unscaledWidth, 100, Math.PI/2, 0, Constants.TOP_BAR_HEIGHT+headerCanvas.height);
				g.beginGradientFill(GradientType.LINEAR, [soundColor.backgroundColor, soundColor.backgroundAccent], [0.8, 0.8], [0, 255], matrix);
				g.drawCircle(unscaledWidth/2, Constants.TOP_BAR_HEIGHT+headerCanvas.height+5-r, r+50);

				matrix.createGradientBox(50, unscaledHeight);
				g.beginGradientFill(GradientType.LINEAR, [soundColor.backgroundAccent, soundColor.backgroundColor], [1.0, 1.0], [0, 255], matrix);
				g.drawRect(0, 0, 50, unscaledHeight);
				
				matrix.createGradientBox(50, unscaledHeight, Math.PI, unscaledWidth-50);
				g.beginGradientFill(GradientType.LINEAR, [soundColor.backgroundAccent, soundColor.backgroundColor], [1.0, 1.0], [0, 255], matrix);
				g.drawRect(unscaledWidth-50, 0, 50, unscaledHeight);
			}

		]]>
	</mx:Script>
	
	<header:HeaderCanvas id="headerCanvas"/>
	<mx:VDividedBox 
		top="{headerCanvas.height}" 
		bottom="0" 
		right="50" left="50" >
		<mx:HDividedBox height="70%" width="100%" >
			<buddies:BuddyCanvas
				backgroundColor="0xffffff"
				id="buddyCanvas"
				minWidth="250"
				width="30%"/>
			<messages:MessageCanvas
				id="chatCanvas"
				minWidth="400"
				width="70%"/>
		</mx:HDividedBox>
		
		<mx:HDividedBox 
				width="100%" height="200"
				includeInLayout="{appModel.showConsole}"
				visible="{appModel.showConsole}">
			<mx:Canvas
					backgroundColor="0xffffff"
					width="100%" height="100%"
					>
				<mx:Label text="Application Log"/>
				<mx:CheckBox x="105" label="enabled" selected="{appModel.logEnabled}" change="appModel.logEnabled = !appModel.logEnabled" />
				<mx:Button label="clear" right="10" click="appModel.resetLog()" />
				<mx:TextArea
					id="logConsole"
					y="30"
					width="100%" height="100%"
					text="{appModel.logText}"
					editable="false"
					updateComplete="logConsole.verticalScrollPosition = logConsole.maxVerticalScrollPosition"
					verticalScrollPolicy="on"
					/>
			</mx:Canvas>
			<mx:Canvas
					backgroundColor="0xffffff"
					width="100%" height="100%"
					>
				<mx:Label text="XML Console"/>
				<mx:CheckBox x="85" label="enabled" selected="{appModel.xmlConsoleEnabled}" change="appModel.xmlConsoleEnabled = !appModel.xmlConsoleEnabled" />
				<mx:Button label="xml input" right="65" click="Swiz.dispatchEvent(new PopUpEvent(PopUpEvent.XML_INPUT_WINDOW))"/>
				<mx:Button label="clear" right="10" click="appModel.resetXmlConsole()" />
				<mx:TextArea
					id="xmlConsole"
					y="30"
					width="100%" height="100%"
					text="{appModel.xmlConsoleText}"
					editable="false"
					updateComplete="xmlConsole.verticalScrollPosition = xmlConsole.maxVerticalScrollPosition"
					verticalScrollPolicy="on"
					/>
			</mx:Canvas>
		</mx:HDividedBox>
	</mx:VDividedBox>

	<mx:Button
		upIcon="{Constants.SendUp}"
		overIcon="{Constants.SendDown}"
		downIcon="{Constants.SendUp}"
		disabledIcon="{Constants.SendDisabled}"
		toolTip="send message"
		focusEnabled="false"
		enabled="{xmppConnected}"
		right="10" y="{30 + headerCanvas.height}"
		width="50" height="50"
		click="Swiz.dispatchEvent(new SendButtonEvent(SendButtonEvent.SEND_CLICKED));"/>

	<buddies:GroupSelectors
		x="9" y="{headerCanvas.height + Constants.TOP_BAR_HEIGHT + Constants.AVATAR_TAB_HEIGHT}" />
		
</mx:Canvas>
